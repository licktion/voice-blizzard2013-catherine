apply plugin: 'de.dfki.mary.voicebuilding-legacy'

voice {
    name = 'blizzard2013-catherine'
    language = 'en'
    region = 'US'
    gender = 'female'
    type = 'unit selection'
    description = 'TBA'
    license {
        name = 'foo'
        shortName = 'foo'
        url = 'bar'
    }
    samplingRate = 16000
}

repositories {
    flatDir {
        dir '../blizzard2013-data'
    }
}

configurations {
    rawdata
}

dependencies {
    rawdata group: 'lessac', name: 'BlackBeauty', version: 'blizzard2013', ext: 'zip'
}

task extractWav(type: Copy) {
    from configurations.rawdata
    into "$buildDir/wav"
    filesMatching '*.zip', { zipFileDetails ->
        logger.info "Unpacking $zipFileDetails.file"
        copy {
            from zipTree(zipFileDetails.file)
            into destinationDir
            eachFile { logger.lifecycle it.name }
        }
        zipFileDetails.exclude()
    }
}

task copyTextAndLabels(type: Copy) {
    from 'src/rawdata'
    into buildDir
}

task prepareVoicebuilding {
    dependsOn extractWav, copyTextAndLabels
}

generateVoiceConfig {
    // TODO need a few more properties here:
    config << [
            acousticModels  : 'duration F0 midF0 rightF0 boundary',
            'boundary.model': 'cart',
            'boundary.data' : "jar:/marytts/voice/$voice.nameCamelCase/bound.tree"
    ]
}

task generateBoundaryTrainingData {
    dependsOn legacyDurationCARTTrainer
    ext.dataFile = file("$buildDir/temp/bound.feats")
    outputs.files dataFile
    doLast {
        dataFile.withWriter { data ->
            legacyDurationCARTTrainer.destFile.eachLine { line ->
                if (line =~ /^[\d.]+\s+_.+/) {
                    data << line
                }
            }
        }
    }
}
