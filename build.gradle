buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'https://oss.jfrog.org/oss-release-local/'
        }
    }
    dependencies {
        classpath group: 'de.dfki.mary', name: 'gradle-marytts-voicebuilding-plugin', version: '0.5.3-SNAPSHOT'
    }
}

apply plugin: 'de.dfki.mary.voicebuilding'

task copyWav(type: Copy) {
    from(project(':blizzard2013-data').projectDir) {
        include 'wav/*.wav'
        rename {
            def m = (it =~ /(.+-)(\d+)(\.wav)/)
            assert m.matches(): it
            sprintf("%s%03d%s", m[0][1], m[0][2] as int, m[0][3]).replaceAll('-', '_')
        }
    }
    into buildDir
}

task copyPitch(type: Copy) {
    from(project(':blizzard2013-data').projectDir) {
        include 'pm/*.Pitch'
    }
    into buildDir
}

task copyLabels(type: Copy) {
    from(project(':blizzard2013-data').projectDir) {
        include 'lab/*.lab'
    }
    into buildDir
}

task extractTextFiles {
    def txtFile = file("src/raw/resources/all.txt")
    def destDir = file("$buildDir/text")
    outputs.dir destDir
    inputs.files txtFile
    doFirst {
        destDir.mkdirs()
    }
    doLast {
        txtFile.eachLine {
            def (basename, content) = it.split(":", 2)
            file("$destDir/${basename}.txt").text = content.trim()
        }
    }
}

task prepareVoicebuilding {
    dependsOn copyWav, copyPitch, copyLabels, extractTextFiles
}

project(':rawdata') {
    apply plugin: 'base'

    repositories {
        flatDir {
            dirs '/proj/marytts.shadow/blizzard2013',
                    "$rootDir.parent/blizzard2013-data"
        }
//        ivy {
//            url 'http://data.cstr.ed.ac.uk/'
//            layout 'pattern', {
//                artifact '[revision]/[organisation]/[module].[ext]'
//            }
//            credentials {
//                user 'blizzard2013User'
//                password 'blizzard2013Password'
//            }
//        }
    }

    dependencies {
        'default' group: 'lessac', name: 'BlackBeauty', version: 'blizzard2013', ext: 'zip'
        'default' group: 'lessac', name: 'Lessac_Blizzard2013_CatherineByers_train', version: 'blizzard2013', ext: 'tar.bz2'
    }
}
