buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'de.dfki.mary:marytts-runtime:5.2-beta1'
    }
}

apply plugin: 'de.dfki.mary.voicebuilding-legacy'

voice {
    name = 'blizzard2013-catherine'
    language = 'en'
    region = 'US'
    gender = 'female'
    type = 'unit selection'
    description = 'TBA'
    license {
        name = 'foo'
        shortName = 'foo'
        url = 'bar'
    }
    samplingRate = 16000
}

repositories {
    flatDir {
        dir '../blizzard2013-data'
    }
}

configurations {
    rawdata
}

dependencies {
    rawdata group: 'lessac', name: 'BlackBeauty', version: 'blizzard2013', ext: 'zip'
}

task extractWav(type: Copy) {
    from configurations.rawdata
    into "$buildDir/rawwav"
    filesMatching '*.zip', { zipFileDetails ->
        logger.info "Unpacking $zipFileDetails.file"
        copy {
            from zipTree(zipFileDetails.file)
            into destinationDir
            eachFile { logger.lifecycle it.name }
        }
        zipFileDetails.exclude()
    }
}

wav {
    dependsOn extractWav
    srcDir = file("$buildDir/rawwav")
}

task copyTextAndLabels(type: Copy) {
    from 'src/rawdata'
    into buildDir
}

legacyInit.dependsOn wav, copyTextAndLabels

generateVoiceConfig {
    // TODO need a few more properties here:
    config << [
            acousticModels  : 'duration F0 midF0 rightF0 boundary',
            'boundary.model': 'cart',
            'boundary.data' : "jar:/marytts/voice/$voice.nameCamelCase/bound.tree"
    ]
}

task generateBoundaryTrainingData {
    dependsOn legacyDurationCARTTrainer
    def durDataFile = file("$buildDir/temp/dur.feats")
    ext.dataFile = file("$buildDir/temp/bound.feats")
    outputs.files dataFile
    doLast {
        dataFile.withWriter { data ->
            durDataFile.eachLine { line ->
                if (line =~ /^[\d.]+\s+_.+/) {
                    data.writeLine line
                }
            }
        }
    }
}

task trainBoundaryCart {
    dependsOn generateBoundaryTrainingData
    def dataFile = file("$buildDir/temp/bound.feats")
    def descFile = file("$buildDir/temp/dur.desc")
    ext.cartFile = file("$buildDir/temp/bound.tree")
    outputs.files cartFile
    doLast {
        exec {
            commandLine = ['wagon', '-data', dataFile, '-desc', descFile, '-stop', 10, '-output', cartFile]
        }
    }
}

import marytts.cart.*
import marytts.cart.io.*
import marytts.unitselection.data.*

task convertBoundaryCartToMaryFormat {
    dependsOn trainBoundaryCart, legacyPhoneFeatureFileWriter
    def inFile = trainBoundaryCart.cartFile
    def outFile = file("$legacyBuildDir/bound.tree")
    outputs.files outFile
    doLast {
        def featureFile = "$legacyBuildDir/phoneFeatures.mry"
        def featureDefinition = FeatureFileReader.getFeatureFileReader(featureFile).featureDefinition
        def wagonCartReader = new WagonCARTReader(LeafNode.LeafType.FloatLeafNode)
        def rootNode = wagonCartReader.load(new BufferedReader(new FileReader(inFile)), featureDefinition)
        def cart = new CART(rootNode, featureDefinition)
        def maryCartWriter = new MaryCARTWriter()
        maryCartWriter.dumpMaryCART(cart, outFile)
    }
}

processResources {
    from convertBoundaryCartToMaryFormat
}
